// - P L U G I N S
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.cloud:spring-cloud-contract-gradle-plugin:2.2.2.RELEASE'
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.2.1.RELEASE")
    }
}
plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'maven-publish'
    id 'project-report'
    id 'idea'
    id "org.sonarqube" version "2.8"
    id "org.hidetake.swagger.generator" version "2.18.2"
}

// - P R O J E C T   D E F I N I T I O N
group = 'org.dimensinfin.printer3d'
version = '0.7.0'
archivesBaseName = 'printer3d-backend'
sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}
configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    deploy
}

// - S O U R C E S
def swaggerGenerated = "$buildDir/generated/"
sourceSets {
    main.java {
        srcDirs += 'src/main/java-client'
        srcDirs += "src/main/java-common"
    }
    test.java {
        srcDirs += "src/test/java-common"
        srcDirs += "src/test/java-datamanagement"
    }
}

// - D E P E N D E N C I E S
apply from: './dependencies.gradle'

// - T A S K S
task cleanResources(type: Delete, dependsOn: clean) {
    group = 'coverage'
    delete fileTree("${projectDir}/src/main/resources/docker") {
        include '**/*.jar'
    }
}

test {
    useJUnitPlatform()
}

// - S W A G G E R   C L I E N T
task swaggerGenerate(type: GenerateSwaggerCode) {
    group = 'openapi'
    language = 'java'

    inputFile = file(new File(projectDir, 'src/spec/printer3d-openapi.yaml'))
    outputDir = file(swaggerGenerated)
    configFile = file('src/spec/openapi-config.json')
}
task swaggerBuild(type: Delete, dependsOn: swaggerGenerate) {
    delete "$swaggerGenerated/docs"
    delete "$swaggerGenerated/gradle"
    delete "$swaggerGenerated/src/test/"
    delete "$swaggerGenerated/src/main/AndroidManifest.xml"
    delete fileTree("$swaggerGenerated").include('*')
}
task swaggerDelete(type: Delete) {
    group = 'openapi'
    delete "$swaggerGenerated"
}
clean.doFirst {
    delete "$swaggerGenerated"
}
dependencies {
    swaggerCodegen 'org.openapitools:openapi-generator-cli:3.0.0'     // or OpenAPI Generator.
    compileOnly 'io.swagger:swagger-annotations:1.5.17'
    compileOnly 'com.squareup.retrofit2:retrofit:2.3.0'
    compileOnly 'com.squareup.retrofit2:converter-gson:2.3.0'
    compileOnly 'com.squareup.retrofit2:converter-scalars:2.3.0'
    compileOnly 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.1'
    compileOnly 'org.apache.httpcomponents:httpclient:4.5.4'
    compileOnly 'org.threeten:threetenbp:1.4.4'
}

// - R U N N I N G
bootRun {
    systemProperty "spring.profiles.active", "acceptance"
}

// - S P R I N G B O O T
springBoot {
    buildInfo()
}
processResources {
    filesMatching('application*') {
        expand(project.properties)
    }
}
